#!/usr/bin/env zsh

local o_help o_devc o_nosvc o_nopest php=(83)

zparseopts -D -F -K -- \
    {h,-help}=o_help \
    {d,-devcontainer}=o_devc \
    {n,-noservices}=o_nosvc \
    -nopest=o_nopest \
    {p,-php}:=php || return

if (( $#o_help )); then
    _laravel_new_print_usage
    return
fi

local services=(mysql redis meilisearch mailpit selenium)
if (( $#o_nosvc )); then
    services=()
fi

local devc
if (( $#o_devc )); then
    devc="--devcontainer"
fi

php=${(q+)php[-1]}

if [[ $# -eq 0 ]]; then
    _laravel_new_print_usage
    return 1
fi

local project=$1
shift

# remaining args, if any, should be services
if [[ $# -ne 0 ]]; then
    services=$@
fi

docker run --rm \
    --pull=always \
    -v "$(pwd)":/opt \
    -w /opt \
    laravelsail/php${php}-composer:latest \
    bash -c "laravel new $project --no-interaction && cd $project && php ./artisan sail:install --with=${(j:,:)services} ${devc}"

cd $project

[[ ${#services} -gt 0 ]] && eval "./vendor/bin/sail pull ${(j: :)services}"
./vendor/bin/sail build

# install pest
(( ! $#o_nopest )) && _laravel_new_pest

if sudo -n true 2>/dev/null; then
    sudo chown -R $USER: .
else
    echo -e "\033[1mEnter password to make final adjustments to application's permissions.${NC}"
    echo ""
    sudo chown -R $USER: .
    echo ""
fi

echo -e "\033[1mGet started with:${NC} cd ${project} && ./vendor/bin/sail up -d"
